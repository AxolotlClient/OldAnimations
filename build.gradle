plugins {
	id 'java'
	id 'fabric-loom' version "1.4.+"
	id 'ploceus' version "1.4.+"
	id 'maven-publish'
	id 'com.modrinth.minotaur' version "2.+"
	id 'io.freefair.lombok' version "8.1.+"
	id "dev.yumi.gradle.licenser" version "1.0.+"
}

group = project.maven_group
version = project.version
archivesBaseName = "AxolotlClient-OldAnimations"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
	maven {
		url = "https://moehreag.duckdns.org/maven/releases"
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
	}
	mavenLocal()
	mavenCentral()
}

loom {
	mods {
		"axolotlclient-oldanimations" {
			sourceSet("main")
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_18}"
	mappings "net.ornithemc:feather:${project.mappings_18}"

	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader}"

	modCompileOnly("io.github.axolotlclient:AxolotlClient:${project.client}+${project.minecraft_18}") {
		transitive = false
	}
	modCompileOnly("io.github.axolotlclient:common:${project.client}") {
		transitive = false
	}
	ploceus.dependOsl(project.osl)
	modImplementation ("io.github.axolotlclient:AxolotlClient-config:${project.config}+${project.minecraft_18}"){
		exclude group: "org.lwjgl"
		exclude group: "org.slf4j"
	}
	include "io.github.axolotlclient:AxolotlClient-config:${project.config}+${project.minecraft_18}"
	modImplementation ("io.github.axolotlclient.AxolotlClient-config:AxolotlClientConfig-common:${project.config}"){
		exclude group: "org.lwjgl"
		exclude group: "org.slf4j"
	}

	api("org.lwjgl:lwjgl-nanovg:3.3.2")
	runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.2:natives-linux')
	runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.2:natives-linux-arm64')
	runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.2:natives-windows')
	runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.2:natives-windows-arm64')
	runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.2:natives-macos')
	runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.2:natives-macos-arm64')

	modCompileOnly("com.github.zarzelcow:legacy-lwjgl3:1.1.1") {
		exclude group: 'org.lwjgl', module: 'lwjgl-glfw'
		exclude group: 'org.lwjgl', module: 'lwjgl-openal'
		exclude group: 'org.lwjgl', module: 'lwjgl-opengl'
		exclude group: 'org.lwjgl', module: 'lwjgl'
		exclude group: 'net.fabricmc'
	}
	modRuntimeOnly("com.github.zarzelcow:legacy-lwjgl3:1.1.1:all-remapped") {
		exclude group: 'org.lwjgl', module: 'lwjgl-glfw'
		exclude group: 'org.lwjgl', module: 'lwjgl-openal'
		exclude group: 'org.lwjgl', module: 'lwjgl-opengl'
		exclude group: 'org.lwjgl', module: 'lwjgl'
		exclude group: 'net.fabricmc'
	}

	modImplementation "com.terraformersmc:modmenu:0.1.0+mc1.8.9"
}

processResources {
	inputs.property 'version', version

	filesMatching('fabric.mod.json') {
		expand "version": version
	}
}

java {
	withSourcesJar()
}

license {
	rule file('HEADER')
	include '**/*.java'
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = archivesBaseName
			from components.java
		}
	}

	repositories {
		maven {
			name = "owlMaven"
			def repository = project.version.toString().contains("beta") || project.version.toString().contains("alpha") ? "snapshots" : "releases"
			url = "https://moehreag.duckdns.org/maven/" + repository
			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "UD5CuiYt"
	versionNumber = "${project.version}"
	versionType = "release"
	uploadFile = remapJar
	gameVersions.set(["${project.minecraft_18}"])
	loaders.set(["fabric", "quilt"])
	additionalFiles.set([remapSourcesJar])

	// Changelog fetching: Credit LambdAurora.
	// https://github.com/LambdAurora/LambDynamicLights/blob/1ef85f486084873b5d97b8a08df72f57859a3295/build.gradle#L145
	// License: MIT
	def changelogText = file('CHANGELOG.md').text
	def regexVersion = ((String) project.version).split("\\+")[0].replaceAll('\\.', /\\./).replaceAll('\\+', '\\+')
	def changelogRegex = ~"###? ${regexVersion}\\n\\n(( *- .+\\n)+)"
	def matcher = changelogText =~ changelogRegex

	if (matcher.find()) {
		def changelogContent = matcher.group(1)

		def changelogLines = changelogText.split('\n')
		def linkRefRegex = ~'^\\[([A-z0-9 _\\-/+.]+)]: '
		for (int i = changelogLines.length - 1; i > 0; i--) {
			def line = changelogLines[i]
			if ((line =~ linkRefRegex).find())
				changelogContent += '\n' + line
			else break
		}
		changelog = changelogContent
	} else {
		afterEvaluate {
			tasks.modrinth.setEnabled(false)
		}
	}
}
